// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.2"
        classpath "io.freefair.gradle:android-gradle-plugins:4.2.0-m1"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: "io.freefair.android-javadoc"

    checkstyle {
        toolVersion '8.36.2'
        configDirectory = file("${project.rootDir}/codecheck")
    }

    task checkstyle(type: Checkstyle) {
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/test/**'
        exclude '**/androidTest/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'
        classpath = files()
    }

    pmd {
        consoleOutput = true
        toolVersion = "6.29.0"
        ruleSetConfig = rootProject.resources.text.fromFile("${rootProject.projectDir}/codecheck/pmd-rules.xml")
        ruleSets = []
        ignoreFailures = true
    }

    task pmd(type: Pmd) {
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/test/**'
        exclude '**/androidTest/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'
        classpath = files()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}